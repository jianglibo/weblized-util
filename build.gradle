buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE")
    }
}

plugins {
    id "org.flywaydb.flyway" version "5.0.7"
}

apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

// https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/html/

eclipse {
    classpath {
        downloadSources = true
    }
}
springBoot {
    mainClassName = 'com.go2wheel.weblizedutil.WeblizedUtilStartPointer'
//    buildInfo()
    buildInfo {
		properties {
			additional = [
				'a': 'alpha',
				'b': 'bravo'
			]
		}
	}
}

bootJar {
	classifier = 'boot'
}

// gradle flywayMigrate
// gradle flywayRepair
flyway {
    url = project.ext['flyway.url']
    user = project.ext['flyway.user']
    password = project.ext['flyway.password']
    locations = project.ext['flyway.locations']
    baselineVersion = '1'
}

archivesBaseName = 'weblized-util'
version = '0.0.2'

sourceSets {
    main {
        resources {
            exclude '**/node_modules/*'
            exclude '**/bower_components/*'
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}


configurations {
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-jdbc')
    compile('org.springframework.boot:spring-boot-starter-quartz')
    compile('org.springframework.boot:spring-boot-starter-aop')
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-validation")
    compile("org.springframework.boot:spring-boot-starter-mail")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-starter-freemarker")
    
    compile("org.springframework.boot:spring-boot-devtools")
    
    // https://mvnrepository.com/artifact/com.profesorfalken/jPowerShell
	compile group: 'com.profesorfalken', name: 'jPowerShell', version: '2.1'
    
    
	compile('org.springframework.shell:spring-shell-starter:2.0.1.RELEASE')
	// https://mvnrepository.com/artifact/com.hierynomus/sshj
	// compile group: 'com.hierynomus', name: 'sshj', version: '0.24.0'
	// https://mvnrepository.com/artifact/com.jcraft/jzlib
	compile group: 'com.jcraft', name: 'jzlib', version: '1.1.3'
	// https://mvnrepository.com/artifact/net.sf.expectit/expectit-core
	compile group: 'net.sf.expectit', name: 'expectit-core', version: '0.9.0'
	// https://mvnrepository.com/artifact/org.apache.httpcomponents/fluent-hc
	compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.5'
	
	
	// https://mvnrepository.com/artifact/mysql/mysql-connector-java
	compile group: 'mysql', name: 'mysql-connector-java', version: '6.0.6'
	
	
	// https://mvnrepository.com/artifact/org.jsoup/jsoup
	compile group: 'org.jsoup', name: 'jsoup', version: '1.11.3'
	
	// https://mvnrepository.com/artifact/org.atteo/evo-inflector
	compile group: 'org.atteo', name: 'evo-inflector', version: '1.2.2'
	
	// https://mvnrepository.com/artifact/org.flywaydb/flyway-core
	compile group: 'org.flywaydb', name: 'flyway-core', version: '5.0.7'
	
	// https://mvnrepository.com/artifact/org.jooq/jooq
	compile group: 'org.jooq', name: 'jooq', version: '3.10.7'
	
	// https://mvnrepository.com/artifact/org.jooq/jooq-meta
	compile group: 'org.jooq', name: 'jooq-meta', version: '3.10.7'
	
	// https://mvnrepository.com/artifact/org.jooq/jooq-codegen
	compile group: 'org.jooq', name: 'jooq-codegen', version: '3.10.7'
	
	
	// https://mvnrepository.com/artifact/com.jcraft/jsch
	compile group: 'com.jcraft', name: 'jsch', version: '0.1.54'
	
	// https://mvnrepository.com/artifact/com.google.guava/guava
	compile group: 'com.google.guava', name: 'guava', version: '25.1-jre'
	
	
	
//	testCompile("junit:junit")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile('org.springframework.security:spring-security-test')
    
   	// https://mvnrepository.com/artifact/org.hsqldb/hsqldb
	// testCompile group: 'org.hsqldb', name: 'hsqldb', version: '2.4.0'
    
}

// task wrapper(type: Wrapper) {
//     gradleVersion = '4.6'
// }

task copyFilesToArchive(type: Copy) {
    from "${buildDir}/../explained.docx", "${buildDir}/../Vagrantfile", "${buildDir}/../start.sh","${buildDir}/../start.bat", "${buildDir}/../stop.bat", "${buildDir}/../killapp.bat", "${buildDir}/../src/main/resources/application.properties"
    
    from("${buildDir}/../") {
        include "notingit/**", "templates/**", "static/**"
    }
    from "${buildDir}/libs/weblized-util-" + project['version'] +  "-boot.jar"
    
    into "${buildDir}/archive/"
}

task packmyzip(type: Zip) {
    archiveName = project['archivesBaseName'] + '-' + project['version'] + ".zip"
    destinationDir = file("${buildDir}/dist")

    from "${buildDir}/archive"
}

task writeVersionFile {
   doLast {
	   def props = new Properties()
	   props["version"] = project.version
	   props["build-time"] = java.time.Instant.now().toString()
	   def f = file("${project['buildDir']}/../src/main/resources/app.version");
	   println f
	   def writer = new FileWriter(f)
	   
	   try {
	      props.store(writer, 'Some comment')
	      writer.flush()
	   } finally {
	      writer.close()
	   }
	}
}

task hello {
    doLast {
        println 'Hello world!'
    }
}

ext {
    freactFolder = "src/main/resources/cache-forever/freact"
    templatesFolder = "src/main/resources/templates"
    // templatesFolder = "src/test/resources/mail"
}

// https://docs.gradle.org/current/userguide/working_with_files.html#sec:locating_files

task freact {
	doLast {
	    FileTree cssFiles = fileTree(templatesFolder) {
	        include "**/*.html"
	    }
	
	    def jss = files(file(freactFolder).listFiles()).filter { File f -> 
	        f.name.endsWith(".js")
	    }
	    // /freact/main.cb98d71c.js
	    String regex = '/freact/main\\.[^.]+\\.js'
	
	    jss.each { File jsf ->
	        cssFiles.each { File htmlFile ->
	            String content = htmlFile.getText("UTF-8")
	            def c = content.findAll(regex).size();
	            if (c > 0) {
	                content = content.replaceAll(regex, "/freact/${jsf.name}");
	                htmlFile.write(content, "UTF-8")
	            }
	        }
	    }
    }
}

task combo {
	doLast {
	    FileTree htmlFiles = fileTree(templatesFolder) {
	        include "**/*.html"
	    }
	
	    // /freact/main.cb98d71c.js
	    String regex = '"/combo/[0-9.]+\\?';

		htmlFiles.each { File htmlFile ->
			String content = htmlFile.getText("UTF-8")
			def c = content.findAll(regex).size();
			if (c > 0) {
				content = content.replaceAll(regex, "\"/combo/${project.version}?");
				htmlFile.write(content, "UTF-8")
			}
		}
	}
    
}
// bootBuildInfo.dependsOn writeVersionFile
bootJar.dependsOn bootBuildInfo 
copyFilesToArchive.dependsOn bootJar
packmyzip.dependsOn copyFilesToArchive
test.dependsOn bootBuildInfo
// gradle test --tests *TestUpgradeUtil
